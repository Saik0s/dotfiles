# Function for safe sourcing of files
function include {
    [[ -s "$1" ]] && source "$1" || echo "$1 not found"
}

# In case a plugin adds a redundant path entry, remove duplicate entries
# from PATH
#
# This snippet is from Mislav MarohniÄ‡ <mislav.marohnic@gmail.com>'s
# dotfiles repo at https://github.com/mislav/dotfiles

dedupe_path() {
    typeset -a paths result
    paths=($path)

    while [[ ${#paths} -gt 0 ]]; do
        p="${paths[1]}"
        shift paths
        [[ -z ${paths[(r)$p]} ]] && result+="$p"
    done

    export PATH=${(j+:+)result}
}

# Start

export COMPLETION_WAITING_DOTS="true"
export BYOBU_PREFIX=/usr/local

export PATH="$HOME/.rbenv/bin:$PATH"
export PATH="$HOME/.jenv/bin:$PATH"

eval "$(rbenv init -)"
eval "$(jenv init -)"

# start zgen
include $HOME/.zsh/zgen-setup.zsh

fpath=(/usr/local/share/zsh/site-functions $fpath)
fpath=(/usr/local/share/zsh-completions $fpath)

# set some history options
setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt hist_verify

# Share your history across all your terminal windows
setopt share_history

# set some more options
setopt pushd_ignore_dups

# Keep a ton of history.
export HISTSIZE=1000
export SAVEHIST=1000
export HISTFILE=~/.zsh_history
export HISTIGNORE="ls:cd:cd -:pwd:exit:date:* --help"

# Long running processes should return time after they complete. Specified
# in seconds.
export REPORTTIME=2
export TIMEFMT="%U user %S system %P cpu %*Es total"

# Expand aliases inline - see http://blog.patshead.com/2012/11/automatically-expaning-zsh-global-aliases---simplified.html
globalias() {
    if [[ $LBUFFER =~ ' [A-Z0-9]+$' ]]; then
        zle _expand_alias
        zle expand-word
    fi
    zle self-insert
}

zle -N globalias

bindkey " " globalias
bindkey "^ " magic-space           # control-space to bypass completion
bindkey -M isearch " " magic-space # normal space during searches

# Speed up autocomplete, force prefix mapping
zstyle ':completion:*' accept-exact '*(N)'
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.zsh/cache
zstyle -e ':completion:*:default' list-colors 'reply=("${PREFIX:+=(#bi)($PREFIX:t)*==34=34}:${(s.:.)LS_COLORS}")';

# Load any custom zsh completions we've installed
if [ -d ~/.zsh-completions ]; then
    for completion in ~/.zsh-completions/*
    do
        source "$completion"
    done
fi

include $HOME/.fzf.zsh

#export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_DEFAULT_OPTS='--height 70% --reverse --border --preview "head -100 {}" '

# export ANDROID_HOME="$HOME/Library/Android/sdk"
# export PATH="$ANDROID_HOME/platform-tools":$PATH
# export PATH="$HOME/flutter/bin":$PATH
# export PATH=$HOME/Documents/Projects/flutter/bin:$PATH

fpath=(~/.zsh/funcs $fpath)
autoload _custom_check_aliases _clean _update _movelink _commit _git_ignore_remove

# Load custom functions
include $HOME/.zsh/functions.zsh

# Load custom aliases
include $HOME/.zsh/aliases.zsh

# Load private
include $HOME/.zsh/private.zsh

#autoload -Uz add-zsh-hook
#add-zsh-hook -D preexec _custom_check_aliases
#add-zsh-hook preexec _custom_check_aliases

export FORK_XCODE_WRITING=true
export PATH="/usr/local/sbin:$PATH"

dedupe_path

export EMAIL="tarasenko.igor.v@gmail.com"
export COMPANY_APPLE_ID=$EMAIL
export ENTERPRISE_APPLE_ID=$EMAIL
export MATCH_USERNAME=$EMAIL

export LANG="en_US.UTF-8"

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

export PATH="$HOME/.yarn/bin:$HOME/.config/yarn/global/node_modules/.bin:$PATH"

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"
